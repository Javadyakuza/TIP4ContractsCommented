pragma ever-solidity ^0.6.12;

pragma AbiHeader expire;
pragma AbiHeader time;
pragma AbiHeader pubkey;

import "./interfaces/IAcceptNftBurnCallback.tsol";
import "./interfaces/IBurnableCollection.tsol";
import "./NftWithRoyalty.tsol";
import "./TIP4_2/TIP4_2Collection.tsol";
import "./TIP4_3/TIP4_3Collection.tsol";
import "./access/OwnableInternal.tsol";
import "./TIP4_royalty/structures/IRoyaltyStructure.tsol";

/**
 * @title Collection
 * @dev This contract represents the main functionality for the existence of an Nft collection on the network.
 * It combines the functionalities from other collection contracts and encompasses them all.
 *
 * This contract holds general information about the Nft collection, such as the total supply and codes of the deployable contracts.
 * Its main functionality is to handle the process of deploying the "Nft" and { IndexBasis } contracts and also handle the destroying process of them.
 * Notice that the { IndexBasis } contract will be destroyed directly,
 * while the destruction (burning) of the "Nft" contract is handled by the "Nft" contract itself. The collection contract only updates its state based on that.
 *
 * Additionally, it provides the users' addresses and code hashes of the relevant contracts, such as
 * {IndexBasis} and "Nft", by calculating them instead of storing them.
 *
 * The collection contract is also enabled to batch mint the Nfts.
 *
 */
contract Collection is TIP4_2Collection, TIP4_3Collection, IBurnableCollection, OwnableInternal, IRoyaltyStructure {

    uint64 static nonce_;
    uint8 constant value_is_less_than_required = 104;

    uint128 _remainOnNft;

    /// @dev Total minted Nfts (active or non-active)
    uint256 _totalMinted;

    /**
     * @param codeNft { NftWithRoyalty } contract code as a TvmCell.
     * @param codeIndex { Index } contract code as a TvmCell.
     * @param codeIndexBasis { IndexBasis } contract code as a TvmCell.
     * @param owner The address of the collection owner.
     * @param remainOnNft The number of EVERs that will remain after the entire mint process is completed on the Nft contract.
     * @param json The JSON metadata object as a string.
     */
    constructor(
        TvmCell codeNft,
        TvmCell codeIndex,
        TvmCell codeIndexBasis,
        address owner,
        uint128 remainOnNft,
        string json
    )
        public
        OwnableInternal(owner)
        TIP4_1Collection(codeNft)
        TIP4_2Collection(json)
        TIP4_3Collection(codeIndex, codeIndexBasis)
    {
        tvm.accept();
        tvm.rawReserve(1 ever, 0);
        _remainOnNft = remainOnNft;
    }

    /**
     * @dev This function is utilized to get the depth of the Nft code.
     * @return The depth of the Nft code.
     */
    function codeDepth() public view returns(uint16) {
        return (_buildNftCode(address(this)).depth());
    }

    /**
     * @dev This function handles the minting process of a single Nft.
     *
     * @param _owner The address of the Nft owner.
     * @param _json The unique Nft related JSON metadata object as a string.
     * @param _royalty The royalty information for the Nft.
     *
     * The minting process cost is paid by the sender.

     * ** preconditions **
     *
     * - The sender must be the owner of the collection contract.
     * - The attached value to the incoming message must be more than _remainOnNft + _indexDeployValue * 2 + 0.3 ever.
     *
     * ** Postconditions **
     *
     * - The collection contract balance will reserve 1 ever and the "_mint" function will be called.
     * - If any condition is not met, the transaction will be reverted.
     */
    function mintNft(address _owner, string _json, Royalty _royalty) public virtual onlyOwner {
        require(msg.value > _remainOnNft + _indexDeployValue * 2 + 0.3 ever, value_is_less_than_required);
        tvm.rawReserve(1 ever, 0);
        _mintNft(_owner, _json, 0, 128, _royalty);
    }

    /**
     * @dev This function is utilized to get the total number of minted Nfts, including active and non-active (burnt) Nfts.
     * @return count The total number of minted Nfts.
     */
    function totalMinted() external view responsible returns (uint256 count) {
        return {value: 0, flag: 64, bounce: false} (_totalMinted);
    }

    /**
     * @dev This function will handle the batch minting process of multiple Nfts.
     *
     * @param _owner The address of the Nft owner.
     * @param _jsons The array of JSON metadata objects as strings for each unique Nft id.
     * @param _royalty The royalty information for the Nfts.
     *
     * @dev Notice that if batch minting the Nfts, a constant royalty information and owner will be considered for all the minting Nfts.
     *
     * ** preconditions **
     *
     * - The sender must be the owner of the collection contract.
     * - The attached value to the incoming message must be more than (_remainOnNft + 3 ever) * jsonsArray.length() + 1 ever.
     *
     * ** Postconditions **
     *
     * - The collection contract balance will reserve 1 ever and the "_mint" function will be called jsonArray.length() times.
     * - If any condition is not met, the transaction will be reverted.
     *
     *
     */
    function batchMintNft(address _owner, string[] _jsons, Royalty _royalty) public virtual onlyOwner {
        require(msg.value > (_remainOnNft + 3 ever) * _jsons.length + 1 ever, value_is_less_than_required);
        tvm.rawReserve(1 ever, 0);
        for ((string _json) : _jsons) {
            _mintNft(_owner, _json, 3 ever, 0, _royalty);
        }
    }

    /**
     * @dev This function deploys (mints) a new Nft contract.
     *
     * @param owner The address of the Nft owner.
     * @param json The JSON metadata object as a string for the Nft.
     * @param value The value to be transferred to the Nft contract as the initial value.
     * @param royalty The royalty information for the specific Nft id.
     *
     * @dev This function will first build the { NftWithRoyalty } code as a TvmCell and then its state using the generated code and the unique id. Accordingly, it will
     * deploy a new Nft contract and emit the { NftCreated } event.
     *
     * Notice that the new id of the Nft will be the { totalMinted } value and not the { totalSupply }.
     *
     * The address of the newly minted Nft can be fetched using the { NftAddress } function from the { TIP4_1Collection } and
     * its related id as the input.
     */
    function _mintNft(address owner, string json, uint128 value, uint16 flag, Royalty royalty) internal virtual {
        uint256 id = uint256(_totalMinted);
        _totalMinted++;
        _totalSupply++;
        TvmCell codeNft = _buildNftCode(address(this));
        TvmCell stateNft = _buildNftState(codeNft, id);
        address NftAddr = new NftWithRoyalty{stateInit: stateNft, value: value, flag: flag}(
            owner,
            msg.sender,
            _remainOnNft,
            json,
            _indexDeployValue,
            _indexDestroyValue,
            _codeIndex,
            royalty
        );
        emit NftCreated(id, NftAddr, owner, owner, msg.sender);
    }

    /**
     *  @dev This function is utilized to set the number of EVERs that will remain after the entire mint process is completed on the Nft contract.
     *
     * @param remainOnNft The number of EVERs that will remain on the Nft.
     *
     * ** preconditions **
     *
     * - The sender must be the owner.
     *
     * ** Postconditions **
     *
     * - If the sender is not the owner, the transaction will be reverted.
     */
    function setRemainOnNft(uint128 remainOnNft) external virtual onlyOwner {
        _remainOnNft = remainOnNft;
    }

    /// @dev Overrides standard method, because Nft contract is changed.
    /// @dev see{ TIP4_1Collection-_buildNftState }
    function _buildNftState(TvmCell code, uint256 id)
        internal
        pure
        virtual
        override (TIP4_2Collection, TIP4_3Collection)
        returns (TvmCell)
    {
        return tvm.buildStateInit({contr: NftWithRoyalty, varInit: {_id: id}, code: code});
    }

    /**
     * @dev This function is used to resolve the code hash of the { Index } contract based on the related Nft owner and the associated collection address.
     *
     * @param collection The address of the collection.
     * @param owner The address of the Nft owner.
     *
     * @dev The collection address must be zero if looking for all of the Nfts owned related to the user's { Index } contract code hash.
     * If an actual collection address is passed as the input, the Nfts owned by the user related to the { Index } contract code hash from that specific collection will be returned.
     *
     * @return The resolved code hash.
     *
     */
    function resolveIndexCodeHash(address collection, address owner) public view returns (uint256 hash) {
        TvmCell code = _buildIndexCode(collection, owner);
        return tvm.hash(code);
    }

    /**
     * @dev This function will update the collection contract state and handle the callbacks defined operations after an Nft gets burnt.
     *
     * @param _id The ID of the unique Nft.
     * @param _owner The address of the Nft owner.
     * @param _manager The address of the Nft manager.
     * @param _sendGasTo The address to send change back.
     * @param _callbackTo The address of the callback.
     * @param _callbackPayload The TvmCell for the callback payload.
     *
     * @dev If there is any callback defined, the callback receiver must implement the { IAcceptNftBurnCallback } interface. see { IAcceptNftBurnCallback }
     *
     * ** preconditions **
     *
     * - The sender must be the associated ID's Nft contract.
     *
     * ** Postconditions **
     *
     * - The { totalSupply } will be reduced by one.
     * - { NftBurned } event will be emitted.
     * - If any callbacks are defined, their operations will be handled.
     * - If there are no callbacks defined, the remaining gas will be sent to { _sendGasTo }.
     * - If the sender is not the valid Nft contract, the transaction will be reverted.
     */
    function acceptNftBurn(
        uint256 _id,
        address _owner,
        address _manager,
        address _sendGasTo,
        address _callbackTo,
        TvmCell _callbackPayload
    ) external override {
        require(msg.sender.value != 0 && _resolveNft(_id) == msg.sender, 100);
        _totalSupply--;
        emit NftBurned(_id, msg.sender, _owner, _manager);
        if (_callbackTo.value != 0) {
            IAcceptNftBurnCallback(_callbackTo).onAcceptNftBurn{
                value: 0,
                flag: 64 + 2,
                bounce: false
            }(
                address(this),
                _id,
                msg.sender,
                _owner,
                _manager,
                _sendGasTo,
                _callbackPayload
            );
        } else {
            _sendGasTo.transfer({
                value: 0,
                flag: 64 + 2,
                bounce: false
            });
        }
    }
}